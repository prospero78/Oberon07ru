(* Модуль для работы с узлами
   BSD-2 *)

МОДУЛЬ модЗвеноСущ;

ИМПОРТ мСтр := модСтроки,
   мПам := модПамять;

КОНСТ
   lxIDENT = -4;
   бСправа = ИСТИНА;
   бСлева = ЛОЖЬ;
   
ТИПЫ
   туЗвеноСущ* = УКАЗАТЕЛЬ НА ЗАПИСЬ
         левый*, правый*: туЗвеноСущ;
         цКлСлово*: ЦЕЛОЕ;
         стрИмя*: мСтр.тСтрока
      КОНЕЦ;

ПЕРЕМ
   сущности: МАССИВ 256 ИЗ туЗвеноСущ;

ПРОЦЕДУРА Добавить*(стрИмя_: мСтр.тСтрока): туЗвеноСущ;
   ПЕРЕМ
      звено_текущ, звено_выход: туЗвеноСущ;

      ПРОЦЕДУРА Сущность_Новая(бСущ: БУЛЕВО);
         НАЧАЛО
            НОВ(звено_выход);
            мПам.Мало(звено_выход = ПУСТО);
            звено_выход.стрИмя := стрИмя_;
            звено_выход.цКлСлово := lxIDENT;
            звено_выход.левый := ПУСТО;
            звено_выход.правый := ПУСТО;
            ЕСЛИ (бСущ = бСправа) ТОГДА
               звено_текущ.правый := звено_выход
            ИНАЧЕ
               звено_текущ.левый := звено_выход
            КОНЕЦ
         КОНЕЦ Сущность_Новая;
      
      ПРОЦЕДУРА Сущность_справа;
         НАЧАЛО
            ЕСЛИ звено_текущ.правый # ПУСТО ТОГДА
               звено_текущ := звено_текущ.правый
            ИНАЧЕ
               Сущность_Новая(бСправа)
            КОНЕЦ
         КОНЕЦ Сущность_справа;
         
      ПРОЦЕДУРА Сущность_слева;
         НАЧАЛО
            ЕСЛИ звено_текущ.левый # ПУСТО ТОГДА
               звено_текущ := звено_текущ.левый
            ИНАЧЕ
               Сущность_Новая(бСлева)
            КОНЕЦ
         КОНЕЦ Сущность_слева;

   НАЧАЛО
      звено_выход := ПУСТО;
      звено_текущ := сущности[НЛИТ(стрИмя_[0])];
      ПОВТОРЯТЬ
         ЕСЛИ стрИмя_ > звено_текущ.стрИмя ТОГДА
            Сущность_справа
         АЕСЛИ стрИмя_ < звено_текущ.стрИмя ТОГДА
            Сущность_слева
         ИНАЧЕ
            звено_выход := звено_текущ
         КОНЕЦ
      ПОКАНЕ звено_выход # ПУСТО
      ВЕРНУТЬ звено_выход
   КОНЕЦ Добавить;

ПРОЦЕДУРА Сущностей_Всего*():ЦЕЛОЕ;
   НАЧАЛО
      ВЕРНУТЬ LEN(сущности)
   КОНЕЦ Сущностей_Всего;

ПРОЦЕДУРА СущностиПункт_Уст*(счётчик_:ЦЕЛОЕ; сущн_:туЗвеноСущ);
   НАЧАЛО
      сущности[счётчик_] := сущн_
   КОНЕЦ СущностиПункт_Уст;

КОНЕЦ модЗвеноСущ.
