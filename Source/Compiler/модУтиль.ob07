(*
      Copyright 2013 Krotov Anton

      This file is part of Compiler.

      Compiler is free software: you can redistribute it and/or modify
      it under the terms of the GNU General Public License as published by
      the Free Software Foundation, either version 3 of the License, or
      (at your option) any later version.

      Compiler is distributed in the hope that it will be useful,
      but WITHOUT ANY WARRANTY; without even the implied warranty of
      MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
      GNU General Public License for more details.

      You should have received a copy of the GNU General Public License
      along with Compiler. If not, see <http://www.gnu.org/licenses/>.
      *)

МОДУЛЬ модУтиль;

ИМПОРТ mSys := SYSTEM,
   мАпи := модАПИ,
   мКонст := модКонстанты,
   мФайл := модФайл,
   мКонс := модКонсоль,
   мПам := модПамять,
   мСтр := модСтроки,
   мОт := модОтладка;

ПЕРЕМ
   мцПараметры: МАССИВ мКонст.цМаксПарам, 2 ИЗ ЦЕЛОЕ;
   цПарамВсего*: ЦЕЛОЕ;
   исх_строка : мСтр.тСтрока; (* не используется *)

ПРОЦЕДУРА ЕслиБесконеч*(пдвХ: ДЛИНВЕЩ): БУЛЕВО;
   ПЕРЕМ
      лнВерх, лнНиз: SET;
   НАЧАЛО
      mSys.GET(mSys.ADR(пдвХ), лнНиз);
      mSys.GET(mSys.ADR(пдвХ) + 4, лнВерх);
      ВЕРНУТЬ (лнВерх * {20..30} = {20..30}) & (лнВерх * {0..19} = {}) & (лнНиз * {0..31} = {})
   КОНЕЦ ЕслиБесконеч;

ПРОЦЕДУРА ЛитераПоАдр_Получ(пцАдр: ЦЕЛОЕ): ЛИТ;
   ПЕРЕМ
      ллВыход: ЛИТ;
   НАЧАЛО
      mSys.GET(пцАдр, ллВыход)
      ВЕРНУТЬ ллВыход
   КОНЕЦ ЛитераПоАдр_Получ;

ПРОЦЕДУРА Параметры_Проверить(пцПарамСчёт: ЦЕЛОЕ);
   ПЕРЕМ
      ллЛитера: ЛИТ;
      лцМаркер, лцПарамВсего: ЦЕЛОЕ;

   ПРОЦЕДУРА Условия_Изменить(пцА, пцБ, пцВход: ЦЕЛОЕ);
      НАЧАЛО
         лцМаркер := пцВход;
         ВЫБОР ллЛитера ИЗ
            |0X: лцМаркер := 6 (* ФИКСМЕ!!! Это что за магия?? *)
            |1X..20X: лцМаркер := пцА
            |22X: лцМаркер := пцБ
         ИНАЧЕ
         КОНЕЦ
         КОНЕЦ Условия_Изменить;

   НАЧАЛО
      лцПарамВсего := мАпи.GetCommandLine();
      лцМаркер := 0;
      ПОКА (пцПарамСчёт < мКонст.цМаксПарам) & (лцМаркер # 6) ДЕЛАТЬ
         ллЛитера := ЛитераПоАдр_Получ(лцПарамВсего);
         ВЫБОР лцМаркер ИЗ
            |0: Условия_Изменить(0, 4, 1);
               ЕСЛИ лцМаркер = 1 ТОГДА
                  мцПараметры[пцПарамСчёт, 0] := лцПарамВсего
               КОНЕЦ
            |4: Условия_Изменить(5, 0, 5);
               ЕСЛИ лцМаркер = 5 ТОГДА
                  мцПараметры[пцПарамСчёт, 0] := лцПарамВсего
               КОНЕЦ
            |1: Условия_Изменить(0, 3, 1);
               ЕСЛИ лцМаркер В {0, 6} ТОГДА
                  мцПараметры[пцПарамСчёт, 1] := лцПарамВсего - 1;
                  ДОБ(пцПарамСчёт)
               КОНЕЦ
            |3, 5: Условия_Изменить(лцМаркер, 1, лцМаркер);
               ЕСЛИ лцМаркер = 6 ТОГДА
                  мцПараметры[пцПарамСчёт, 1] := лцПарамВсего - 1;
                  ДОБ(пцПарамСчёт)
               КОНЕЦ
         ИНАЧЕ
         КОНЕЦ;
         ДОБ(лцПарамВсего)
      КОНЕЦ;
      цПарамВсего := пцПарамСчёт - 1
   КОНЕЦ Параметры_Проверить;

ПРОЦЕДУРА СтрПарам*(ПЕР пстрПарам: МАССИВ ИЗ ЛИТ; пцПарамВсего: ЦЕЛОЕ);
   ПЕРЕМ
      лцАдрЛит, j, лцПарамСтрДлина: ЦЕЛОЕ;
      литера: ЛИТ;
   НАЧАЛО
      j := 0;
      ЕСЛИ пцПарамВсего <= цПарамВсего ТОГДА
         лцПарамСтрДлина := ДЛИНА(пстрПарам) - 1;
         лцАдрЛит := мцПараметры[пцПарамВсего, 0];
         ПОКА (j < лцПарамСтрДлина) & (лцАдрЛит <= мцПараметры[пцПарамВсего, 1]) ДЕЛАТЬ
            литера := ЛитераПоАдр_Получ(лцАдрЛит);
            ЕСЛИ литера # 22X ТОГДА
               пстрПарам[j] := литера;
               ДОБ(j)
            КОНЕЦ;
            ДОБ(лцАдрЛит)
         КОНЕЦ
      КОНЕЦ;
      пстрПарам[j] := 0X
   КОНЕЦ СтрПарам;

ПРОЦЕДУРА Параметры_Линукс;
   ПЕРЕМ
      лцУказКмд, лцПарам, лцИтер, лцСтр: ЦЕЛОЕ;
      ллЛит: ЛИТ;
   НАЧАЛО
      мОт.Печать("модУтиль.Параметры_Линукс");
      лцУказКмд := мАпи.GetCommandLine();
      mSys.GET(лцУказКмд, цПарамВсего);
      mSys.GET(лцУказКмд + 4, лцПарам);
      ДЛЯ лцИтер := 0 ДО цПарамВсего - 1 ДЕЛАТЬ
         mSys.GET(лцПарам + лцИтер * 4, лцСтр);
         мцПараметры[лцИтер, 0] := лцСтр;
         ПОВТОРЯТЬ
            mSys.GET(лцСтр, ллЛит);
            ДОБ(лцСтр)
         ПОКАНЕ ллЛит = 0X;
         мцПараметры[лцИтер, 1] := лцСтр - 1
      КОНЕЦ;
      ВЫЧ(цПарамВсего)
   КОНЕЦ Параметры_Линукс;

ПРОЦЕДУРА Настроить; (* + *)

   ПРОЦЕДУРА ЛитераПослед_Получ(ПЕР адр_: ЦЕЛОЕ);
      НАЧАЛО
         ПОКА ЛитераПоАдр_Получ(адр_) # 0X ДЕЛАТЬ
            ДОБ(адр_)
         КОНЕЦ;
         ВЫЧ(адр_) (* контрольное вычитание после выхода из цикла *)
      КОНЕЦ ЛитераПослед_Получ;

   НАЧАЛО
      мОт.Печать("модУтиль.Настроить");
      ЕСЛИ мКонст.бВиндоус ТОГДА
         Параметры_Проверить(0)
      АЕСЛИ мКонст.бКолибри ТОГДА
         Параметры_Проверить(1);
         мцПараметры[0, 0] := мАпи.GetName();
         мцПараметры[0, 1] := мцПараметры[0, 0];
         ЛитераПослед_Получ(мцПараметры[0, 1])
      АЕСЛИ мКонст.бЛинукс ТОГДА
         Параметры_Линукс
      КОНЕЦ
   КОНЕЦ Настроить;

НАЧАЛО (* + *)
   Настроить
КОНЕЦ модУтиль.
