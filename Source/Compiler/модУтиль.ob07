(*
      Copyright 2013 Krotov Anton

      This file is part of Compiler.

      Compiler is free software: you can redistribute it and/or modify
      it under the terms of the GNU General Public License as published by
      the Free Software Foundation, either version 3 of the License, or
      (at your option) any later version.

      Compiler is distributed in the hope that it will be useful,
      but WITHOUT ANY WARRANTY; without even the implied warranty of
      MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
      GNU General Public License for more details.

      You should have received a copy of the GNU General Public License
      along with Compiler. If not, see <http://www.gnu.org/licenses/>.
      *)

МОДУЛЬ модУтиль;

ИМПОРТ mSys := SYSTEM,
   мАпи := модАПИ,
   мКонст := модКонстанты,
   мФайл := модФайл,
   мКонс := модКонсоль,
   мПам := модПамять,
   мСтр := модСтроки,
   мОт := модОтладка;

ПЕРЕМ
   масПараметры: МАССИВ мКонст.макс_парам, 2 ИЗ ЦЕЛОЕ;
   параметры_всего*: ЦЕЛОЕ;
   исх_строка : мСтр.тСтрока; (* не используется *)

ПРОЦЕДУРА ЕслиБесконеч*(x_: ДЛИНВЕЩ): БУЛЕВО;
   ПЕРЕМ
      верх, низ: SET;
   НАЧАЛО
      mSys.GET(mSys.ADR(x_), низ);
      mSys.GET(mSys.ADR(x_) + 4, верх);
      ВЕРНУТЬ (верх * {20..30} = {20..30}) & (верх * {0..19} = {}) & (низ * {0..31} = {})
   КОНЕЦ ЕслиБесконеч;

ПРОЦЕДУРА ЛитераПоАдр_Получ(адр_: ЦЕЛОЕ): ЛИТ;
   ПЕРЕМ
      выход: ЛИТ;
   НАЧАЛО
      mSys.GET(адр_, выход)
      ВЕРНУТЬ выход
   КОНЕЦ ЛитераПоАдр_Получ;

ПРОЦЕДУРА Параметры_Проверить(счётчик_: ЦЕЛОЕ);
   ПЕРЕМ
      литера: ЛИТ;
      маркер, парам_число: ЦЕЛОЕ;

   ПРОЦЕДУРА Условия_Изменить(A, B, C: ЦЕЛОЕ);
      НАЧАЛО
         маркер := C;
         ВЫБОР литера ИЗ
            |0X: маркер := 6
            |1X..20X: маркер := A
            |22X: маркер := B
         ИНАЧЕ
         КОНЕЦ
         КОНЕЦ Условия_Изменить;

   НАЧАЛО
      парам_число := мАпи.GetCommandLine();
      маркер := 0;
      ПОКА (счётчик_ < мКонст.макс_парам) & (маркер # 6) ДЕЛАТЬ
         литера := ЛитераПоАдр_Получ(парам_число);
         ВЫБОР маркер ИЗ
            |0: Условия_Изменить(0, 4, 1);
               ЕСЛИ маркер = 1 ТОГДА
                  масПараметры[счётчик_, 0] := парам_число
               КОНЕЦ
            |4: Условия_Изменить(5, 0, 5);
               ЕСЛИ маркер = 5 ТОГДА
                  масПараметры[счётчик_, 0] := парам_число
               КОНЕЦ
            |1: Условия_Изменить(0, 3, 1);
               ЕСЛИ маркер В {0, 6} ТОГДА
                  масПараметры[счётчик_, 1] := парам_число - 1;
                  ДОБ(счётчик_)
               КОНЕЦ
            |3, 5: Условия_Изменить(маркер, 1, маркер);
               ЕСЛИ маркер = 6 ТОГДА
                  масПараметры[счётчик_, 1] := парам_число - 1;
                  ДОБ(счётчик_)
               КОНЕЦ
         ИНАЧЕ
         КОНЕЦ;
         ДОБ(парам_число)
      КОНЕЦ;
      параметры_всего := счётчик_ - 1
   КОНЕЦ Параметры_Проверить;

ПРОЦЕДУРА СтрПарам*(ПЕР стр_: МАССИВ ИЗ ЛИТ; парам_число_: ЦЕЛОЕ);
   ПЕРЕМ
      i, j, парам_размер: ЦЕЛОЕ;
      литера: ЛИТ;
   НАЧАЛО
      j := 0;
      ЕСЛИ парам_число_ <= параметры_всего ТОГДА
         парам_размер := ДЛИНА(стр_) - 1;
         i := масПараметры[парам_число_, 0];
         ПОКА (j < парам_размер) & (i <= масПараметры[парам_число_, 1]) ДЕЛАТЬ
            литера := ЛитераПоАдр_Получ(i);
            ЕСЛИ литера # 22X ТОГДА
               стр_[j] := литера;
               ДОБ(j)
            КОНЕЦ;
            ДОБ(i)
         КОНЕЦ
      КОНЕЦ;
      стр_[j] := 0X
   КОНЕЦ СтрПарам;

ПРОЦЕДУРА Параметры_Линукс;
   ПЕРЕМ
      указ_кмд, парам, итер, цСтр: ЦЕЛОЕ;
      литера: ЛИТ;
   НАЧАЛО
      мОт.Печать("модУтиль.Параметры_Линукс");
      указ_кмд := мАпи.GetCommandLine();
      mSys.GET(указ_кмд, параметры_всего);
      mSys.GET(указ_кмд + 4, парам);
      ДЛЯ итер := 0 ДО параметры_всего - 1 ДЕЛАТЬ
         mSys.GET(парам + итер * 4, цСтр);
         масПараметры[итер, 0] := цСтр;
         ПОВТОРЯТЬ
            mSys.GET(цСтр, литера);
            ДОБ(цСтр)
         ПОКАНЕ литера = 0X;
         масПараметры[итер, 1] := цСтр - 1
      КОНЕЦ;
      ВЫЧ(параметры_всего)
   КОНЕЦ Параметры_Линукс;

ПРОЦЕДУРА Настроить; (* + *)

   ПРОЦЕДУРА ЛитераПослед_Получ(ПЕР адр_: ЦЕЛОЕ);
      НАЧАЛО
         ПОКА ЛитераПоАдр_Получ(адр_) # 0X ДЕЛАТЬ
            ДОБ(адр_)
         КОНЕЦ;
         ВЫЧ(адр_) (* контрольное вычитание после выхода из цикла *)
      КОНЕЦ ЛитераПослед_Получ;

   НАЧАЛО
      мОт.Печать("модУтиль.Настроить");
      ЕСЛИ мКонст.бВиндоус ТОГДА
         Параметры_Проверить(0)
      АЕСЛИ мКонст.бКолибри ТОГДА
         Параметры_Проверить(1);
         масПараметры[0, 0] := мАпи.GetName();
         масПараметры[0, 1] := масПараметры[0, 0];
         ЛитераПослед_Получ(масПараметры[0, 1])
      АЕСЛИ мКонст.бЛинукс ТОГДА
         Параметры_Линукс
      КОНЕЦ
   КОНЕЦ Настроить;

НАЧАЛО (* + *)
   Настроить
КОНЕЦ модУтиль.
