(*
    Copyright 2013 Krotov Anton

    This file is part of Compiler.

    Compiler is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    Compiler is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Compiler. If not, see <http://www.gnu.org/licenses/>.
  *)

МОДУЛЬ модУтиль;

ИМПОРТ mSys := SYSTEM,
    API,
    мКонст := модКонстанты,
    мТипы := модТипы,
    мФайл := модФайл,
    мКонс := модКонсоль,
    мПам := модПамять;

ПЕРЕМ

  масПараметры: МАССИВ мКонст.макс_парам, 2 ИЗ INTEGER;
  ParamCount*, Line*, Unit*: INTEGER;
  (* win, kos, lnx: BOOLEAN; *)

ПРОЦЕДУРА Бесконеч*(x_: LONGREAL): BOOLEAN;
  ПЕРЕМ
    h, l: SET;
  НАЧАЛО
    mSys.GET(mSys.ADR(x_), l);
    mSys.GET(mSys.ADR(x_) + 4, h);
    ВЕРНУТЬ (h * {20..30} = {20..30}) & (h * {0..19} = {}) & (l * {0..31} = {})
  КОНЕЦ Бесконеч;

ПРОЦЕДУРА Сивол_Получ(adr_: INTEGER): CHAR;
  ПЕРЕМ
   res: CHAR;
  НАЧАЛО
    mSys.GET(adr_, res)
    ВЕРНУТЬ res
  КОНЕЦ Сивол_Получ;

ПРОЦЕДУРА ParamParse(count: INTEGER);
   ПЕРЕМ c: CHAR; cond, p: INTEGER;

   ПРОЦЕДУРА ChangeCond(A, B, C: INTEGER);
      НАЧАЛО
         cond := C;
         ВЫБОР c ИЗ
            |0X: cond := 6
            |1X..20X: cond := A
            |22X: cond := B
         ИНАЧЕ
         КОНЕЦ
      КОНЕЦ ChangeCond;

   НАЧАЛО
      p := API.GetCommandLine();
      cond := 0;
      ПОКА (count < мКонст.макс_парам) & (cond # 6) ДЕЛАТЬ
         c := Сивол_Получ(p);
         ВЫБОР cond ИЗ
            |0: ChangeCond(0, 4, 1);
               ЕСЛИ cond = 1 ТОГДА
                  масПараметры[count, 0] := p
               КОНЕЦ
            |4: ChangeCond(5, 0, 5);
                ЕСЛИ cond = 5 ТОГДА
                  масПараметры[count, 0] := p
                КОНЕЦ
            |1: ChangeCond(0, 3, 1);
                ЕСЛИ cond IN {0, 6} ТОГДА
                  масПараметры[count, 1] := p - 1;
                  INC(count)
                КОНЕЦ
            |3, 5: ChangeCond(cond, 1, cond);
                   ЕСЛИ cond = 6 ТОГДА
                     масПараметры[count, 1] := p - 1;
                     INC(count)
                   КОНЕЦ
         ИНАЧЕ
         КОНЕЦ;
         INC(p)
      КОНЕЦ;
      ParamCount := count - 1
   КОНЕЦ ParamParse;

ПРОЦЕДУРА ParamStr*(ПЕР str: МАССИВ ИЗ CHAR; n: INTEGER);
  ПЕРЕМ
   i, j, len: INTEGER; c: CHAR;
  НАЧАЛО
    j := 0;
    ЕСЛИ n <= ParamCount ТОГДА
      len := LEN(str) - 1;
      i := масПараметры[n, 0];
      ПОКА (j < len) & (i <= масПараметры[n, 1]) ДЕЛАТЬ
        c := Сивол_Получ(i);
        ЕСЛИ c # 22X ТОГДА
          str[j] := c;
          INC(j)
        КОНЕЦ;
        INC(i)
      КОНЕЦ
    КОНЕЦ;
    str[j] := 0X
  КОНЕЦ ParamStr;

ПРОЦЕДУРА UnitLine*(newUnit, newLine: INTEGER);
  НАЧАЛО
    Unit := newUnit;
    Line := newLine
  КОНЕЦ UnitLine;

ПРОЦЕДУРА min*(a, b: INTEGER): INTEGER;
  ПЕРЕМ Res: INTEGER;
  НАЧАЛО
    ЕСЛИ a < b ТОГДА
      Res := a
    ИНАЧЕ
      Res := b
    КОНЕЦ
    ВЕРНУТЬ Res
  КОНЕЦ min;

ПРОЦЕДУРА Align*(n: INTEGER): INTEGER;
    ВЕРНУТЬ (4 - n MOD 4) MOD 4
  КОНЕЦ Align;

ПРОЦЕДУРА CAP(x: CHAR): CHAR;
  НАЧАЛО
    ЕСЛИ (x >= "a") & (x <= "z") ТОГДА
      x := CHR(ORD(x) - 32)
    КОНЕЦ
    ВЕРНУТЬ x
  КОНЕЦ CAP;

ПРОЦЕДУРА streq*(a, b: МАССИВ ИЗ CHAR): BOOLEAN;
  ПЕРЕМ i: INTEGER;
  НАЧАЛО
    i := -1;
    ПОВТОРЯТЬ
      INC(i)
    ПОКАНЕ (CAP(a[i]) # CAP(b[i])) ИЛИ (a[i] = 0X) ИЛИ (b[i] = 0X)
    ВЕРНУТЬ a[i] = b[i]
  КОНЕЦ streq;

ПРОЦЕДУРА Push*(this: мТипы.туСписок; item: мТипы.туПункт);
  НАЧАЛО
    ЕСЛИ this.счетчик = 0 ТОГДА
      this.предыдущ := item;
      item.предыдущ := ПУСТО
    ИНАЧЕ
      this.следующ.следующ := item;
      item.предыдущ := this.следующ
    КОНЕЦ;
    INC(this.счетчик);
    this.следующ := item;
    item.следующ := ПУСТО
  КОНЕЦ Push;

ПРОЦЕДУРА Insert*(this: мТипы.туСписок; item, prev: мТипы.туПункт);
  НАЧАЛО
    ЕСЛИ prev # this.следующ ТОГДА
      item.следующ := prev.следующ;
      item.предыдущ := prev;
      prev.следующ := item;
      item.следующ.предыдущ := item;
      INC(this.счетчик)
    ИНАЧЕ
      Push(this, item)
    КОНЕЦ
  КОНЕЦ Insert;

ПРОЦЕДУРА Clear*(this: мТипы.туСписок);
  НАЧАЛО
    this.предыдущ := ПУСТО;
    this.следующ := ПУСТО;
    this.счетчик := 0
  КОНЕЦ Clear;

ПРОЦЕДУРА Список_Создать*(): мТипы.туСписок;
  ПЕРЕМ
    nov: мТипы.туСписок;
  НАЧАЛО
    NEW(nov)
    ВЕРНУТЬ nov
  КОНЕЦ Список_Создать;

ПРОЦЕДУРА Revers(ПЕР str: мТипы.тСтрока);
  ПЕРЕМ a, b: INTEGER; c: CHAR;
  НАЧАЛО
    a := 0;
    b := LENGTH(str) - 1;
    ПОКА a < b ДЕЛАТЬ
      c := str[a];
      str[a] := str[b];
      str[b] := c;
      INC(a);
      DEC(b)
    КОНЕЦ
  КОНЕЦ Revers;

ПРОЦЕДУРА Split*(FName: мТипы.тСтрока; ПЕР Path, Name, Ext: мТипы.тСтрока);
   ПЕРЕМ
   i, j, k: INTEGER;
   НАЧАЛО
      i := LENGTH(FName) - 1;
      j := 0;
      ПОКА (i >= 0) & (FName[i] # API.Slash) ДЕЛАТЬ
         Name[j] := FName[i];
         DEC(i);
         INC(j)
      КОНЕЦ;
      Name[j] := 0X;
      Revers(Name);
      j := 0;
      k := LENGTH(Name) - 1;
      ПОКА (k >= 0) & (Name[k] # ".") ДЕЛАТЬ
         Ext[j] := Name[k];
         DEC(k);
         INC(j)
      КОНЕЦ;
      ЕСЛИ k >= 0 ТОГДА
         Name[k] := 0X;
         Ext[j] := ".";
         INC(j)
      ИНАЧЕ
         j := 0
      КОНЕЦ;
      Ext[j] := 0X;
      Revers(Ext);
      j := i;
      ДЛЯ i := 0 ДО j ДЕЛАТЬ
         Path[i] := FName[i]
      КОНЕЦ;
      Path[j + 1] := 0X
   КОНЕЦ Split;

ПРОЦЕДУРА PChar*(s: МАССИВ ИЗ CHAR): INTEGER;
    ВЕРНУТЬ mSys.ADR(s)
  КОНЕЦ PChar;

ПРОЦЕДУРА LinuxParam;
  ПЕРЕМ
     p, i, str: INTEGER; c: CHAR;
  НАЧАЛО
    p := API.GetCommandLine();
    mSys.GET(p, ParamCount);
    mSys.GET(p + 4, p);
    ДЛЯ i := 0 ДО ParamCount - 1 ДЕЛАТЬ
      mSys.GET(p + i * 4, str);
      масПараметры[i, 0] := str;
      ПОВТОРЯТЬ
        mSys.GET(str, c);
        INC(str)
      ПОКАНЕ c = 0X;
      масПараметры[i, 1] := str - 1
    КОНЕЦ;
    DEC(ParamCount)
  КОНЕЦ LinuxParam;

ПРОЦЕДУРА Настроить;
   ПЕРЕМ
      p, count: INTEGER;

    ПРОЦЕДУРА last(ПЕР p: INTEGER);
      НАЧАЛО
         ПОКА Сивол_Получ(p) # 0X ДЕЛАТЬ
            INC(p)
         КОНЕЦ;
           DEC(p)
      КОНЕЦ last;

  НАЧАЛО
    ЕСЛИ мКонст.win ТОГДА
      мКонс.Вывод_Открыть;
      ParamParse(0)
    АЕСЛИ мКонст.kol ТОГДА
      ParamParse(1);
      масПараметры[0, 0] := API.GetName();
      масПараметры[0, 1] := масПараметры[0, 0];
      last(масПараметры[0, 1])
    АЕСЛИ мКонст.lin ТОГДА
      мКонс.Вывод_Открыть;
      LinuxParam
    КОНЕЦ
  КОНЕЦ Настроить;

НАЧАЛО
  Настроить
КОНЕЦ модУтиль.
