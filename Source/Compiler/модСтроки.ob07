(* содержит определение строки и всякие полезняшки для обработки строк
      BSD-2
   *)
МОДУЛЬ модСтроки;

ИМПОРТ мКонст := модКонстанты,
   mSys := SYSTEM,
   мАпи := модАпи;

ТИПЫ
   тСтрока* = МАССИВ мКонст.макс_дл_пути ИЗ СИМВ;

ПРОЦЕДУРА Заглав*(симв_: СИМВ): СИМВ;
    НАЧАЛО
        ЕСЛИ (симв_ >= "a") & (симв_ <= "z") ТОГДА
             симв_ := CHR(ORD(симв_) - 32)
        АЕСЛИ (симв_ >= "а") & (симв_ <= "я") ТОГДА
             симв_ := CHR(ORD(симв_) - 32)
        АЕСЛИ (симв_="ё") ТОГДА
                симв_ := "Ё"
        ИНАЧЕ
        КОНЕЦ;
        ВЕРНУТЬ симв_
    КОНЕЦ Заглав;

ПРОЦЕДУРА Сложить*(стр_лев_, стр_прав_: МАССИВ ИЗ СИМВ; ПЕР выход_: МАССИВ ИЗ СИМВ);
   ПЕРЕМ
      счёт_лев, счёт_прав: ЦЕЛОЕ;
   НАЧАЛО
      счёт_лев := 0;
      ПОКА (стр_лев_[счёт_лев] # 0X) & (счёт_лев - 1 < LEN(выход_)) ДЕЛАТЬ
         выход_[счёт_лев] := стр_лев_[счёт_лев];
         ДОБ(счёт_лев)
      КОНЕЦ;
      счёт_прав := 0;
      ПОКА (стр_прав_[счёт_прав] # 0X) & (счёт_прав + счёт_лев - 1 < LEN(выход_)) ДЕЛАТЬ
         выход_[счёт_прав + счёт_лев] := стр_прав_[счёт_прав];
         ДОБ(счёт_прав)
      КОНЕЦ;
      выход_[счёт_прав + счёт_лев] := 0X
   КОНЕЦ Сложить;

ПРОЦЕДУРА Реверс*(ПЕР стр_: тСтрока);
   ПЕРЕМ
      нач, кон: ЦЕЛОЕ;
      симв: СИМВ;
   НАЧАЛО
      нач := 0;
      кон := ДЛИНА(стр_) - 1;
      ПОКА нач < кон ДЕЛАТЬ
         симв := стр_[нач];
         стр_[нач] := стр_[кон];
         стр_[кон] := симв;
         ДОБ(нач);
         ВЫЧ(кон)
      КОНЕЦ
   КОНЕЦ Реверс;

ПРОЦЕДУРА Разделить*(файл_имя: тСтрока; ПЕР путь_, имя_, расшир_: тСтрока);
   ПЕРЕМ
      счёт, j, k: ЦЕЛОЕ;
   НАЧАЛО
      счёт := ДЛИНА(файл_имя) - 1;
      j := 0;
      
      (* =====выделение имени файла с конца до слэша======= *)
      ПОКА (счёт >= 0) & (файл_имя[счёт] # мАпи.Slash) ДЕЛАТЬ
         имя_[j] := файл_имя[счёт];
         ВЫЧ(счёт);
         ДОБ(j)
      КОНЕЦ;
      имя_[j] := 0X;
      
      (* ======выделение расширения файла====== *)
      Реверс(имя_);
      j := 0;
      k := ДЛИНА(имя_) - 1;
      ПОКА (k >= 0) & (имя_[k] # ".") ДЕЛАТЬ
         расшир_[j] := имя_[k];
         ВЫЧ(k);
         ДОБ(j)
      КОНЕЦ;
      ЕСЛИ k >= 0 ТОГДА
         имя_[k] := 0X;
         расшир_[j] := ".";
         ДОБ(j)
      ИНАЧЕ
         j := 0
      КОНЕЦ;
      расшир_[j] := 0X;
      Реверс(расшир_);
      
      (* =====выделение пути к файлу====== *)
      j := счёт;
      ДЛЯ счёт := 0 ДО j ДЕЛАТЬ
         путь_[счёт] := файл_имя[счёт]
      КОНЕЦ;
      путь_[j + 1] := 0X
   КОНЕЦ Разделить;

КОНЕЦ модСтроки.
